plugins {
    id 'org.jetbrains.intellij' version '1.5.3'
    id 'org.jetbrains.changelog' version '1.2.1'
    id 'com.github.ldenisey.setversions' version '1.0.0'
}

apply plugin: 'org.jetbrains.changelog'

group = 'com.javampire'
version = '2.1.2-SNAPSHOT'

sourceCompatibility = 1.11
targetCompatibility = 1.11
sourceSets.main.java.srcDirs = ['src/main/java', 'src/main/gen']

ext {
    pluginVerifierIdeVersions = "CL-2020.1, " +
            "GO-2020.1, " +
            "IIC-2020.1.3, " +
            "PCC-2020.1, " +
            "PS-2020.1, " +
            "RD-2020.1, " +
            "WS-2020.1"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

intellij {
    version.set("212-EAP-SNAPSHOT")

    //plugins.set(['psiviewer:203-SNAPSHOT']) // For debugging the parser only

    updateSinceUntilBuild.set(false)

    patchPluginXml {
        sinceBuild.set('201.6668')
        untilBuild.set('')

        changeNotes.set(provider { changelog.getLatest().toHTML() })
    }
}

// Read more: https://github.com/JetBrains/gradle-changelog-plugin
changelog {
    version.set(project.version)
    header.set(project.version)
    headerParserRegex.set(~/\d\.\d\.\d(-SNAPSHOT)?/)
    unreleasedTerm.set("Unreleased")
}

runPluginVerifier {
    ideVersions = (List) pluginVerifierIdeVersions.split(',')
}

publishPlugin {
    token = System.getenv("intellijPublishToken")
}
