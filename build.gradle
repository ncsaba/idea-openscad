plugins {
    id 'org.jetbrains.intellij' version '0.7.2'
    id 'org.jetbrains.changelog' version '1.2.0'
    id 'com.github.ldenisey.setversions' version '1.0.0'
}

apply plugin: 'org.jetbrains.changelog'

group 'com.javampire'
version '2.1.1-SNAPSHOT'

sourceCompatibility = 1.11
targetCompatibility = 1.11
sourceSets.main.java.srcDirs = ['src/main/java', 'src/main/gen']

ext {
    pluginVerifierIdeVersions = "CL-2020.1, " +
            "GO-2020.1, " +
            "IIC-2020.1.3, " +
            "PCC-2020.1, " +
            "PS-2020.1, " +
            "RD-2020.1, " +
            "WS-2020.1"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

intellij {
    //plugins = ['psiviewer:203-SNAPSHOT'] // For debugging the parser only
    updateSinceUntilBuild = false

    patchPluginXml {
        sinceBuild '201.6668'
        untilBuild ''

        changeNotes({ changelog.getLatest().toHTML() })
    }
}

// Read more: https://github.com/JetBrains/gradle-changelog-plugin
changelog {
    version = project.version
    header = { project.version }
    headerParserRegex = ~/\d\.\d\.\d(-SNAPSHOT)?/
    unreleasedTerm = "Unreleased"
}

runPluginVerifier {
    ideVersions = pluginVerifierIdeVersions
}

publishPlugin {
    token = System.getenv("intellijPublishToken")
}
